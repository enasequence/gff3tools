plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id "org.hidetake.ssh" version "2.2.0"
    id "com.diffplug.spotless" version "6.11.0"
    id "com.gradleup.shadow" version "8.3.0"
}

archivesBaseName = 'gff3tools'
group = 'uk.ac.ebi.ena'
version = '1.0'

ext.codonDeployBaseDir = "/hps/software/users/tburdett/ena/jar/$archivesBaseName"
ext.codonDeployArtifactDir = codonDeployBaseDir

spotless {
    encoding 'UTF-8'
    lineEndings 'UNIX'
    java {
        licenseHeader '''/*
 * Copyright 2025 EMBL - European Bioinformatics Institute
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 */
'''
        importOrder 'java', 'javax', 'org', 'com', '', 'uk'
        removeUnusedImports()
        palantirJavaFormat()
    }
}


repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://gitlab.ebi.ac.uk/api/v4/groups/enasequence/-/packages/maven"
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation("org.projectlombok:lombok:1.18.30")
    annotationProcessor("org.projectlombok:lombok:1.18.30")
    implementation( 'uk.ac.ebi.ena.sequence:sequencetools:2.+' )
    implementation "io.vavr:vavr:0.10.5"
    implementation "org.jgrapht:jgrapht-core:1.5.2"
    implementation("info.picocli:picocli:4.7.6")
    implementation 'ch.qos.logback:logback-classic:1.3.14'
    implementation 'org.slf4j:slf4j-api:2.0.9'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

remotes {
    codon {
        host = 'codon-ena-slurm.ebi.ac.uk'
        user = 'esa'
        identity = file("${System.properties['user.home']}/.ssh/id_rsa")
        knownHosts = allowAnyHosts
    }
}

test {
    useJUnitPlatform()
}


shadowJar {
    archiveClassifier.set('')
}

application {
    mainClassName = 'uk.ac.ebi.embl.converter.cli.Main'
}

tasks.distTar.dependsOn shadowJar
tasks.distZip.dependsOn shadowJar
tasks.startScripts.dependsOn shadowJar
tasks.startShadowScripts.dependsOn jar

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "https://gitlab.ebi.ac.uk/api/v4/projects/5502/packages/maven"
        }
    }
}


task deploy() {
    dependsOn shadowJar

    doLast {
        deployToRemote(remotes.codon, codonDeployBaseDir, codonDeployArtifactDir)
    }
}

def deployToRemote(remote, deployBaseDir, deployArtifactDir) {
    def assemble = jar

    uploadAssembleAndCreateSymlink(assemble, deployBaseDir , deployArtifactDir,remote)

    displaySymlink(getSymlinkFullName(deployBaseDir , assemble ), remote)
}

def uploadAssembleAndCreateSymlink(assemble, symlinkDir, codonDeployArtifactDir, remote) {

    def symlinkFullName = getSymlinkFullName(symlinkDir , assemble)
    def symlinkTarget = getSymlinkTarget(codonDeployArtifactDir,  assemble)


    printSeparationLine()
    println("\tsymlinkDir:'$symlinkDir'\tuploadAssembleDir:'$codonDeployArtifactDir'")
    println("\tsymlinkName:'$symlinkFullName'\tsymlinkTargetName:'$symlinkTarget'")
    printSeparationLine()

    uploadAssemble(assemble , codonDeployArtifactDir , remote)
    createSymlink(symlinkFullName , symlinkTarget , remote)
}

def displaySymlink(symlink , remote){
    ssh.run {
        session(remote) {
            def result = execute "ls -l $symlink", ignoreError: true
            println "Current symlink: $result"
        }
    }
}

def uploadAssemble(assemble , uploadDir , remote){
    ssh.run {
        session(remote) {
            println "Uploading assembly:'$assemble.archiveName' to $remote.host:$uploadDir"
            put(assemble.archivePath.absolutePath, uploadDir)
        }
    }
}

def createSymlink(symlink , symlinkTarget , remote){
    ssh.run {
        session(remote) {
            println ("Creating symbolic link:'$symlink', target:'$symlinkTarget', remote:'$remote.host'")
            execute "ln -s -f $symlinkTarget $symlink"
        }
    }
}

static String getAssembleBaseName(assemble){
    return assemble.getBaseName() + "." + assemble.getExtension()
}

static String getSymlinkFullName(symlinkDir, assemble){
    return "$symlinkDir/" + getAssembleBaseName(assemble)
}

static String getSymlinkTarget(uploadAssembleDir, assemble){
    return "$uploadAssembleDir/$assemble.archiveName"
}

def printSeparationLine(){
    println("*" * 100)
}
